## 2.3 Groovy 棒球 ##
&#160; &#160; &#160; &#160;图2.4为我们展示了一个叫Groovy棒球的web应用。它是通过你给出一个日期，这个页面就会创造出一个职棒大联盟的地图，这张地图上有比赛的地点、双方球队。比赛结果也会在小板子上列出来。你也可以调整这个日期，它就会用Ajax更新这些数据。

![图2.4](http://i.imgur.com/kxOqTpm.png)

&#160; &#160; &#160; &#160;有一些功能通过JavaScript调用谷歌地图的API完成，例如绘制地图、添加比赛地点标记等。对于给定日期的比赛结果，则通过Ajax调用原生JavaScript类库完成。这些代码我将在以后展示。我将着眼于这个应用中Groovy的部分。

![图2.5](http://i.imgur.com/dYWpaVC.png)

&#160; &#160; &#160; &#160;这个应用是简单的，但他必须有一个合理的结构，所以我将要分几部分来完成它。第一步当然是要去收集MLB场馆的地理信息，并且将它们存在数据库中，就像图2.5展示的那样。

在这一部分中，我将要涉及：
- 展示一下POGO
- 访问一个REST式的web服务
- groovy.sql.Sql 类

下一步是访问在线的分数库，然后将结果粘贴返回的XML文件，就像图2.6那样。

![图2.6](http://i.imgur.com/YXAQE84.png)

在这个阶段中，我们将要讨论：
- 从数据库中读取数据
- 从互联网中下载信息
- 粘贴XML

最后，我需要将返回的数据发向视图层，并且保证它可以理解，就像图2.7那样

![图2.7](http://i.imgur.com/SM0gVZS.png)

在这个阶段，我们将要涉及：
- 用groovylet
- 生成xml

我将开始第一部分，创建POGO，将数据保存到数据库中。
### 2.3.1 数据库数据与旧Groovy对象的展示
这些网上的比赛结果都是以每场比赛的场馆为中心的。谷歌地图是根据所给地理位置的经度纬度绘制标记的。因为比赛场馆不会移动，所以它的位置可以提前保存在某种持久性的结构之中。在这里我用MySQL数据库，你用任何数据库都可以。

我将要写一段脚本来收集每个MLB比赛场馆的必要信息，计算它的经度纬度，并在数据库的表中储存它们。我将要以一个场馆对象为开始。
#### 比赛场馆的POGO
在Java中，我们一般会给这种类叫做java对象或者POJO。在Groovy中，我们将叫它Groovy对象或者是POGO。下面将展示这个Stadium类。
###### 代码清单2.2 Stadium.groovy:一个包含比赛场馆信息的POGO
```groovy
package beans
class Stadium {
	int id
	String name
	String city
	String state
	String team
	double latitude
	double longitude
	String toString() { "($team,$name,$latitude,$longitude)" }
}
```
如果你用过java，你将会发现这里少了一些东西。少了分号可能还没有让你那么惊讶。真正让你震惊的应该是缺少了public和private这些访问权限限定符。在Groovy中如果你不指定访问权限，属性的默认为private，方法默认为public。

你也许也注意到了在Stadium中没有构造方法。在Java中，如果你不添加构造方法，编译器会默认给你一个空的构造方法。在Groovy中，不但给了你空的构造方法，而且还允许你通过键值对的方式指定任意属性的值。

带着这些在脑子里，这是第一部分的脚本，用于填充数据库表中Stadium的位置：
```groovy
def stadiums = []
stadiums <<
	new Stadium(name:'Angel Stadium',city:'Anaheim',state:'CA',team:'ana')
stadiums <<
	new Stadium(name:'Chase Field',city:'Phoenix',state:'AZ',team:'ari')
...
stadiums <<
	new Stadium(name:'Rogers Centre',city:'Toronto',state:'ON',team:'tor')
stadiums <<
	new Stadium(name:'Nationals Park',
	city:'Washington',state:'DC',team:'was')
```
这个*stadium*变量是对一个空的*java.util.ArrayList*的初始化。左移位符*<<*在*Collection*中作为一个附加的方法已经被实现了，接下来的操作就是将每一个MLB场馆对象添加到这个List中。

每个构造器都设置了场馆的*name*、*city*、*state*还有三个字母的球队缩写。没设置的是*latitude*和*longitude*的值。这些将用到Google地理编码（是另一项Google提供的REST式的web服务，类似于我们前面用到的Google图表API）。
>**POGO**  有点类似于Java中的POJO，但是增加了自动生成的getter、setter方法，还有基于map的构造函数。

#### 地理编码
Google地理编码API的文档在

https://developers.google.com/maps/documentation/geocoding/

地理编码能将一个地址转化为经度纬度的形式。要用到Google地理编码你需要组装一个URL，要包括一个地址信息。文档中提到，URL有一个平台

http://maps.googleapis.com/maps/api/geocode/output?parameters

这里*output*的值为*xml*或者*json*，具体的返回类型根据你想返回的类型而定。这个*parameters*属性包含地址也包含一个*sensor*的值。这有个文档中的例子，是谷歌在芒廷维尤 (加利福尼亚州)的地址：

http://maps.googleapis.com/maps/api/geocode/xml?address=1600+Amphitheatre+Parkway,+Mountain+View,+CA&sensor=true_or_false

如果你试图用JavaScript访问地理编码，我将建议你用json（Javascript Object Notation）来展示输出的结果。但是因为我们正在用Groovy，所以我们应该用XML。请求字符串包含两个参数。第一个是地址，它包含街区、城市、州（用","分开）的URL编码值。另一个参数叫*sensor*，它的值当这个请求来自于一个启用了GPS的设备时为true，其他情况为false。

我将要开始编码，第一步就是设一个代表基础URL的变量
```groovy
def base = 'http://maps.googleapis.com/maps/api/geocode/xml?'
```
为了组装查询字符串，我们得拥有一个包含比赛场馆名称、城市、所在州的list：
```groovy
[stadium.name, stadium.city, stadium.state]
```
这些值有可能会包含空格、引号或者其他的标点符号，这些在URL中是违法的。因此我将要对这些值进行URL编码。正如我在上一部分讲的，对一个*list*运用*collect*方法能得到一个包含了已变换值得新*list*。在这种情况下，我将用*java.net.URLEncoder*中的*encode*方法：
```groovy
[stadium.name, stadium.city, stadium.state].collect {
	URLEncoder.encode(it,'UTF-8')
}.join(',')
```
如果你用一个没有虚参的闭包，像这里一样，*list*中的每一个元素都将分配到一个叫*it*的变量。闭包内将要对*name,city,state*执行静态的*encode*方法，对它们进行UTF-8编码。结果将是一个包含已经被重新编码过的值的*list*。最后这些值，奖杯组装到一个以","为分隔符的字符串中。

小心的装配地址。形成一个完整的查询字符串必须得用到像Google图表里那样相同的闭包。完整的程序在这：
```groovy
def url = base + [sensor:false,
	address: [stadium.name, stadium.city, stadium.state].collect {
		URLEncoder.encode(it,'UTF-8')
	}.join(',')
].collect {k,v -> "$k=$v"}.join('&')
```
> ###### 构建一个查询字符串
> *map*参数、*collect*闭包、*join*方法的组合是一个方便的方式来构建一个查询字符串。一个开发者应该能以任何顺序来储存和接受字符串（像在Grails应用中那样）。并将它们用最小的代价转变成查询字符串。

所有这些字符串操作的目的就是构造一个完整的URL，与之前那个例子相似，这个URL可以被发送到Google地理编码上去。

有趣的部分现在开始了。这个地理编码器返回了一个相当大的XML（就不再这里展示了，你可以去看看Google地理编码的文档，网址是：https://developers.google.com/maps/documentation/geocoding/#XML） 。用java来处理XML是相当繁琐的，但幸运的是，对Groovy来说简直是小菜一碟。将URL发送到Google地理编码并且将结果解析成一棵DOM树只用一行
```groovy
def response = new XmlSlurper().parse(url)
```
Groovy有两个类用来解析XML。一个叫*XmlParser*，另一个叫*XmlSlurper*。都能将XML解析成一棵DOM树。但内部的结构和处理方式略有不同，从实际观察来看*XmlSlurper*是更有效的同时花费的内存更少，所以我将会在这里用它。为了提取我需要的结果，我得遍历一遍这颗树。我本可以将XML粘贴出来，为你展示一下它的结构，但是我觉得你看一下这个Groovy的代码你也能很容易的理解。
```groovy
stadium.latitude = response.result[0].geometry.location.lat.toDouble()
stadium.longitude = response.result[0].geometry.location.lng.toDouble()
```
换句话说，*XmlSlurper*返回了DOM树的根，用*response*变量来代表。根有一个子节点叫*result*，它有一个子节点叫*geometry*，它有一个子节点叫*location*，它有两个子节点分别是*lat*和*lng*。有时地理编码会返回多行结果，*result*变成了数组，*result[0]*代表只用第一个。因为在XML中都是字符串，所以我想将结果赋值给*Stadium*中的*double*类型的属性就要调用*toDouble*方法。
> **粘贴XML** 无论你用*XmlParser*还是*XmlSlurper*，从XML中提取数据就意味着必须遍历这个树。

接下来这段代码清单展示了一个完整*Geocoder*类，其中包含了*fillInLatLng*方法，这个方法以*Stadium*作为参数，并将其中的经度纬度属性赋值。
###### 代码清单2.3  Geocoder.groovy：用Google地理编码来计算经度纬度
```groovy
class Geocoder {
	def base = 'http://maps.googleapis.com/maps/api/geocode/xml?'
	def fillInLatLng(Stadium stadium) {
		def url = base + [sensor:false,
			address: [stadium.name, stadium.city, stadium.state].collect {
				URLEncoder.encode(it,'UTF-8')
			}.join(',')
		].collect{k,v -> "$k=$v"}.join('&')
		def response = new XmlSlurper().parse(url)
		stadium.latitude =
			response.result[0].geometry.location.lat.toDouble()
		stadium.longitude =
			response.result[0].geometry.location.lng.toDouble()
		return stadium
	}
}
```
#### groovy.sql.Sql类
回到原来的问题，我想要将一个比赛场馆的信息储存在数据库中。现在我要拿出Groovy类库中又一大法宝——groovy.sql.Sql类。这个类可以连接数据库，并且可以执行SQL语句。我们先来说一下Sql如何实例化：
```groovy
Sql db = Sql.newInstance(
	'jdbc:mysql://localhost:3306/baseball',
	'...username...',
	'...password...',
	'com.mysql.jdbc.Driver'
)
```
Sql类有一个静态的*newInstance*方法，它的参数是JDBC的URL、用户名、密码、驱动类。返回的结果就是数据库的连接。接下来，我们先删除一下*Stadium*表，以防它已经存在了：
```groovy
db.execute "drop table if exists stadium;"
```
*execute*方法可以将传入的SQL字符串在数据库中运行。这里我又用了一次可选的圆括号。

下一步就是创建*Stadium*表，来储存信息：
```groovy
db.execute '''
	create table stadium(
		id int not null auto_increment,
		name varchar(200) not null,
		city varchar(200) not null,
		state char(2) not null,
		team char(3) not null,
		latitude double,
		longitude double,
		primary key(id)
	);
'''
```
三个单引号在Groovy代表一个多行的字符串。三个双引号代表多行的*GString*，它可以用来替换参数，但是这种情况下没必要用。

现在表已经被创建了，是时候向表中插入数据了：
```groovy
Geocoder geo = new Geocoder()
stadiums.each { s ->
	geo.fillInLatLng s
	db.execute """
		insert into stadium(name, city, state, team, latitude, longitude)
		values(${s.name},${s.city},${s.state},
			${s.team},${s.latitude},${s.longitude});
	"""
}
```
在将地理编码实例化之后，遍历容器中的每一个*Stadium*，并为它分配一个局部变量*s*。对于每一个*s*，在计算完经度纬度之后，我执行*insert*语句。因为这句中*values*后边的值要用*${...}*的形式来替换，所以就用到了前面所说的三个双引号。

现在剩下来的工作就是确保接收的值是合理的。下面是*assert*语句：
```groovy
assert db.rows('select * from stadium').size() == stadiums.size()
db.eachRow('select latitude, longitude from stadium') { row ->
	assert row.latitude > 25 && row.latitude < 48
	assert row.longitude > -123 && row.longitude < -71
}
```
第一个*assert*语句检查表中的行数是否与容器中场馆的个数相匹配。接下来的语句调用了*eachRow*方法，仅仅将经度纬度选择出来，并让*row*代表每一行的结果。接下来的两个*assert*就是判断经度是否在25到48之间，纬度是否在-123到-71之间。
> **SQL类** groovy.sql.Sql类 几乎移除了原JDBC全部的样板化代码，并且添加了一些方便的方法。

完整的脚本段展现在这：
###### 代码清单2.4 populate_stadium_data.groovy
```groovy
package service

import groovy.sql.Sql
//用Stadium实例填充list
def stadiums = []
stadiums <<
	new Stadium(name:'Angel Stadium',city:'Anaheim',state:'CA',team:'ana')
...
stadiums <<
	new Stadium(name:'Nationals Park',city:'Washington',
	state:'DC',team:'was')
//访问数据库
Sql db = Sql.newInstance(
	'jdbc:mysql://localhost:3306/baseball',
	'...username...',
	'...password...',
	'com.mysql.jdbc.Driver'
)
db.execute "drop table if exists stadium;"
//多行sql
db.execute '''
	create table stadium(
		id int not null auto_increment,
		name varchar(200) not null,
		city varchar(200) not null,
		state char(2) not null,
		team char(3) not null,
		latitude double,
 		longitude double,
		primary key(id)
	);
'''
Geocoder geo = new Geocoder()
//插入数据库
stadiums.each { s ->
	geo.fillInLatLng s
	db.execute """
		insert into stadium(name,city,state,team,latitude,longitude)
	values(${s.name},${s.city},${s.state},${s.team},${s.latitude}${s.longitude});
"""
}
//检查结果
assert db.rows('select * from stadium').size() == stadiums.size()
db.eachRow('select latitude,longitude from stadium') { row ->
	assert row.latitude > 25 && row.latitude < 48
	assert row.longitude > -123 && row.longitude < -71
}
```
这段脚本收集了每一个MLB比赛场馆的经度纬度，并创建了一个数据库表来保存它们，并将它们填入表中。它只用运行一次，接下来的程序就能使用这个表。复习一下代码，我们用了一个*Stadium*的POGO，一个*list*，一对带闭包的*collect*方法，一个在Groovy脚本中用java的URLEncoder的例子，还有用groovy.sql.Sql的数据库操作。

下一步就是从一个职棒大联盟的网站来收集比赛分数，并且生成XML信息，推送到视图界面。
### 2.3.2 解析XML
职棒大联盟在网上持续的更新棒球比赛的结果。这些信息以XML的形式保存在http://gd2.mlb.com/components/game/mlb/

在那个网站比赛按照日期排列。所以想得到比赛结果，除了这个基础的URL之外，还需要附加的链接，像这样“year_${year}/month_${month}/day_${day}/”，在这里年是4位数字，月和日都是两位数字。那一天的比赛以独立链接的形式被陈列。例如，图2.8就展现了在2007年5月5日的每场比赛。

![图2.8](http://i.imgur.com/aFkS0nX.png)

每场比赛的链接都是如下形式：
gid_${year}_${month}_${day}_${away}mlb_${home}mlb_${num}

*year、month、day*的值和想象的一样。*away*和*home*的值是三个小写的球队缩写字母，*num*的值代表那天的比赛号（1代表第一场比赛，2代表第二场比赛）。每场比赛的链接都包含了一系类比赛，但我对一个叫*boxscore.xml*的文件感兴趣。

为了检索*boxscore.xml*中的比赛信息，我将创建一个叫做*GetGameData*的类。这类将有一些属性，包括基础的URL和每个球队的缩写等。下一个代码列表将展示这个类的一部分。
###### 代码列表2.5 GetGameData类中属性和初始化的部分
```groovy
class GetGameData {
	def day
	def month
	def year
	String base = 'http://gd2.mlb.com/components/game/mlb/'
	Map stadiumMap = [:]//球队的缩写和比赛场馆的映射
	Map abbrevs = [
		ana:'Los Angeles (A)', ari:'Arizona', atl:'Atlanta',
		bal:'Baltimore', bos:'Boston', cha:'Chicago (A)',
		chn:'Chicago (N)', cin:'Cincinnati', cle:'Cleveland',
		col:'Colorado', det:'Detroit', flo:'Florida',
		hou:'Houston', kca:'Kansas City', lan:'Los Angeles (N)',
 		mil:'Milwaukee', min:'Minnesota', nya:'New York (A)',
		nyn:'New York (N)', oak:'Oakland', phi:'Philadelphia',
		pit:'Pittsburgh', sdn:'San Diego', sea:'Seattle',
		sfn:'San Francisco', sln:'St. Louis', tba:'Tampa Bay',
		tex:'Texas', tor:'Toronto', was:'Washington']
 	GetGameData() {
		fillInStadiumMap()
	}
	//从数据库中读出比赛场馆的数据
	def fillInStadiumMap() {
		Sql db = Sql.newInstance(
			'jdbc:h2:build/baseball',
			'org.h2.Driver'
		)
		db.eachRow("select * from stadium") { row ->
			Stadium stadium = new Stadium(
				name:row.name,
				team:row.team,
				latitude:row.latitude,
				longitude:row.longitude
			)
			stadiumMap[stadium.team] = stadium
		}
		db.close()
	}
```
在*abbrevs*的键值对，储存三个字母的球队缩写与球队所在城市的映射。

下一步就是处理*boxscore.xml*了。这有一些随机抽取的简单数据。我随机选的日子是2007年10月28日。下一个清单展现了*boxscore.xml*，但只是展现了一些典型的数据，而不是全部。
###### 代码清单2.6 部分boxscore.xml
```xml
<boxscore game_id="2007/10/28/bosmlb-colmlb-1" game_pk="224026"
	home_sport_code="mlb" away_team_code="bos" home_team_code="col"
	away_id="111" home_id="115" away_fname="Boston Red Sox"
	home_fname="Colorado Rockies" 
	away_sname="Boston" home_sname="Colorado" date="October 28, 2007"
	away_wins="5" away_loss="0" home_wins="0" home_loss="5" status_ind="F">
	<linescore away_team_runs="4" home_team_runs="3"
		away_team_hits="9" home_team_hits="7" away_team_errors="0"
		home_team_errors="0">
		<inning_line_score away="1" home="0" inning="1"/>
		<inning_line_score away="0" home="0" inning="2"/>
		...
		<inning_line_score away="0" home="0" inning="9"/>
	</linescore>
	<pitching team_flag="away" out="27" h="7" r="3" er="3" bb="3"
		so="7" hr="2" bf="37" era="2.60">
		<pitcher id="452657" name="Lester" pos="P" out="17" bf="23" er="0"
			r="0" h="3" so="3" hr="0" bb="3" w="2" l="0" era="0.00" note="(W, 2-0)" />
		<pitcher id="434668" name="Delcarmen" pos="P" out="2" bf="4"
			er="1" r="1" h="2" so="1" hr="1" bb="0" w="0" l="0" era="9.00" note="(H, 2)" />
		...
		<pitcher id="449097" name="Papelbon" pos="P" out="5" bf="5" er="0"
			r="0" h="0" so="1" hr="0" bb="0" w="0" l="0" era="0.00" note="(S, 4)" />
	</pitching>
	<batting team_flag="home" ab="34" r="3" h="7" d="2" t="0" hr="2"
		rbi="3" bb="3" po="27" da="18" so="7" avg=".216" lob="12">
		<batter id="430565" name="Matsui" pos="2B" bo="100" ab="4" po="3"
			r="0" bb="0" a="5" t="0" sf="0" h="1" e="0" d="1" hbp="0" so="1" hr="0"
			rbi="0" lob="2" fldg="1.000" avg=".286" />
		<batter id="466918" name="Corpas" pos="P" bo="101" ab="0" po="0"
			r="0" bb="0" a="1" t="0" sf="0" h="0" e="0" d="0" hbp="0" so="0" hr="0"
			rbi="0" lob="0" fldg="1.000" avg=".000" />
		...
	</batting>
	<pitching team_flag="home" out="27" h="9" r="4" er="4" bb="1"
		so="4" hr="2" bf="34" era="6.91">
		<pitcher id="346871" name="Cook" pos="P" out="18" bf="23" er="3"
			r="3" h="6" so="2" hr="1" bb="0" w="0" l="2" era="4.50" note="(L, 0-2)" />
		...
	</pitching>
	<batting team_flag="away" ab="33" r="4" h="9" d="2" t="0" hr="2"
		rbi="4" bb="1" po="27" da="8" so="4" avg=".322" lob="10">
		<batter id="453056" name="Ellsbury" pos="CF-LF" bo="100" ab="4"
			po="3" r="1" bb="0" a="0" t="0" sf="0" h="2" e="0" d="1" so="1" hr="0"
			rbi="0" lob="2" fldg="1.000" avg=".450" />
		<batter id="456030" name="Pedroia" pos="2B" bo="200" ab="4" po="1"
			r="0" bb="0" a="4" t="0" sf="0" h="0" e="0" d="0" hbp="0" so="0" hr="0"
			rbi="0" lob="2" fldg="1.000" avg=".227" />
		...
	</batting>
...
</boxscore>
```
根元素为*boxscore*，它有很多属性。它有一个子元素叫*linescore*,它显示了每局的得分。这还有*pitching*和*batting*元素，代表客队和主队。

XML文件不是特别复杂，如果你非得用java来处理它，这些java代码就会变得特别复杂难懂。如果你用Groovy，你所做的仅仅就是遍历这棵树。

我们用与上一张解析地理编码相同的方法来解析这些数据。我们需要用月、日、年来组装这个URL，并且处理*boxscore.xml*：
```groovy
def url = base + "year_${year}/month_${month}/day_${day}/"
def game = "gid_${year}_${month}_${day}_${away}mlb_${home}mlb_${num}/
		boxscore.xml"
def boxscore = new XmlSlurper().parse("$url$game")
```
在遍历这棵树之后，就可以提取队名和分数了：
```groovy
def awayName = boxscore.@away_fname
def awayScore = boxscore.linescore[0].@away_team_runs
def homeName = boxscore.@home_fname
def homeScore = boxscore.linescore[0].@home_team_runs
```
“.”代表从父元素到子元素，而“@”代表该元素的属性。
>差一个真理
####生成比赛结果
对于这个Groovy棒球应用，我对结果输出不太感兴趣。但我想将其组装成特定的格式，这样就可以被处于表现层的JavaScript处理了。这意味着我需要返回一个对象，它可以被转化成XML（或者Json）。

这个*GameResult*类就是干这件事的：
```groovy
class GameResult {
	String home
	String away
	String hScore
	String aScore
	Stadium stadium
	String toString() { "$home $hScore, $away $aScore" }
}
```
> **闭包返回值：**最后那个有*$*的表达式就是该闭包的返回值。

这个POGO是对主队、客队、主队得分、客队得分以及比赛场馆的简单包装。之所以我们需要*Stadium*是因为它包含着我们要添加在Google地图上的经度纬度。接下来这段代码，展现了完整的*getGame*方法，它属于代码清单2.5中，GetGameDate类的一部分。
######代码清单2.7 GetGameData.groovy中的getGame方法
```groovy
def getGame(away, home, num) {
	println "${abbrevs[away]} at ${abbrevs[home]} on ${month}/${day}/
		${year}"
	def url = base + "year_${year}/month_${month}/day_${day}/"
	def game = "gid_${year}_${month}_${day}_${away}mlb_${home}mlb_${num}/
			boxscore.xml"
	def boxscore = new XmlParser().parse("$url$game")//处理XML数据
	def awayName = boxscore.@away_fname//提取数据
	def awayScore = boxscore.linescore[0].@away_team_runs
	def homeName = boxscore.@home_fname
	def homeScore = boxscore.linescore[0].@home_team_runs
	println "$awayName $awayScore, $homeName $homeScore (game $num)"
	//创建响应对象
	GameResult result = new GameResult(home:homeName,
		away:awayName,
		hScore:homeScore,
		aScore:awayScore,
		stadium:stadiumMap[home]//用主队的场馆
	)
	return result
}
```
这个方法用*XmlSlurper*来将XML转化成DOM树，提取需要的信息，并且创建返回的实例*GameResult*类。